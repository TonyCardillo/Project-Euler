REMSTART

Euler Project :: Problem 10
Started: 7/22/2009 8:15 AM
Solved:  7/22/2009 9:01 AM

Description:
The sum of the primes below 10 is 2 + 3 + 5 + 7 = 17.
Find the sum of all the primes below two million.


Correct Answer:
142913828922

After Notes:
Wasn't too hard, I ended up not needing the carrying checker.
A few bugs prevented me from solving it faster, but I found them :).
There are a few ways I could optimize it, but it is well within
Project Euler's one-minute rule, so it doesn't matter.
This method solves it in about 5 seconds.

REMEND

` Text properties
set text opaque

` Create an array of the first 2,000,000 numbers
dim sieve(2000000) as boolean

` Fill the array with 1's (0=composite,1=prime)
` All numbers are prime until proven otherwise :)
for reset = 1 to 2000000
   sieve(reset)=1
next reset

` This loop will determine every prime from 2 to 2000000
for num = 2 to 1000000
   for chk = num*2 to 2000000 step num
      sieve(chk)=0
   next chk
next num

` Add all of the primes and keep total of the carry
total as double integer = 0
carry as byte = 0

for add = 2 to 2000000
   if sieve(add)=1
      total = total + add
      if total < 0
         inc carry
         total = 9223372036854775807 + total
      endif
   endif
next add

` Display the result
text 0,0,"The sum of the first 2,000,000 primes is:"
text 0,20,"("+str$(carry)+" * 9223372036854775807) + "+str$(total)
wait key
end







